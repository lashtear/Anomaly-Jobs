#
# Anomaly Jobs LCARS skin v1.0 by Starfleet
# 
# Contributor: Minion@Crossroads
#
# ----------------------------------------------------------------
# A Star Trek-themed skin plug-in for Anomaly Jobs 6.x
# 
# If you do not like the default colors, you can easily modify 
# it by changing: LCARS_COLOR<1-5>
#
# ----------------------------------------------------------------

@switch 
	[ifelse(
		isdbref(setr(0,
			switch(
				first(version()),
				PennMUSH,lsearch(all,eobjects,\[strmatch(name(##),Job Global Object <JGO>)\]),
				RhostMUSH,searchng(object=Job Global Object <JGO>),
				search(object=Job Global Object <JGO>)
			)
		)),
		setq(1,get(%q0/VA)),
		[setq(1,
			switch(
				first(version()),
				PennMUSH,lsearch(all,eobjects,\[strmatch(name(##),Job Database <JD>)\]),
				RhostMUSH,searchng(object=Job Database <JD>),
				search(object=Job Database <JD>)
			)
		)]
		[setq(0,loc(%q1))]
	)]
	[isdbref(%q0)]
	[and(
		isdbref(%q1),
		gte(edit(first(default(%q1/version,0),.),v,),6)
	)]
	=
	0*,{
		think [ansi(hc,ANOMALY JOBS:)] [ansi(hr,No jobs installation found.)];
		think [ansi(hc,ANOMALY JOBS:)] [ansi(hr,Aborting. Cancel this script in your client.)];
		&JOB_VA %#=#-1;
	},
	10*,{
		think [ansi(hc,ANOMALY JOBS:)] [ansi(hr,Not a valid Jobs 6 or later installation.)];
		think [ansi(hc,ANOMALY JOBS:)] [ansi(hr,Aborting. Cancel this script in your client.)];
		&JOB_VA %#=#-1;
	},{
		think [ansi(hc,ANOMALY JOBS:)] Jobs 6 installation found.  Installing skin.;
		&JOB_VA %#=%q1;
	}
-

&LCARS_BREAK [v(JOB_VA)]=
	[switch(%0,,
		[ansi(get(%va/LCARS_COLOR1),%b[repeat(_,u(%va/FN_FLEXWIDTH,77))])]
		%r
		[ansi(get(%va/LCARS_COLOR1),|[repeat(_,u(%va/FN_FLEXWIDTH,77))]|)],
		[ansi(get(%va/LCARS_COLOR1),%b[repeat(_,u(%va/FN_FLEXWIDTH,77))])]
		%r
		[ansi(get(%va/LCARS_COLOR1),|)]
		[ansi(get(%va/LCARS_COLOR1),rjust(ansi(get(%va/LCARS_COLOR2),stripansi(%0)),u(%va/FN_FLEXWIDTH,77),_))]
		[ansi(get(%va/LCARS_COLOR1),|)]
	)]
-

&LCARS_BANNER [v(JOB_VA)]=
	[setq(0,%0)]
	[setq(x,
		u(%va/FN_ITEMIZE,
			map(MAP_NAME,get(%q0/OPENED_BY),%b,|),
			|
		)
	)]
	[setq(y,
		u(%va/FN_ITEMIZE,
			map(MAP_NAME,get(%q0/TAGGED_FOR),%b,|),
			|
		)
	)]
	[ljust(
		[rjust(ansi(get(%va/LCARS_COLOR5),Bucket:),10)]%b
		[u(%va/FN_BUCKETNAME,%q0)],
		div(u(%va/FN_FLEXWIDTH,80),2)
	)]
	[rjust(ansi(get(%va/LCARS_COLOR5),Due On:),12)]%b
	[ifelse(get(%q0/DUE_ON),
		ifelse(
			gt(secs(),get(%q0/DUE_ON)),
			OVERDUE!,
			convsecs(get(%q0/DUE_ON))
		),
		-
	)]
	%r
	[ljust(
		[rjust(ansi(get(%va/LCARS_COLOR5),Title:),10)]%b
		[get(%q0/TITLE)],
		div(u(%va/FN_FLEXWIDTH,80),2)
	)]
	[rjust(ansi(get(%va/LCARS_COLOR5),Status:),12)]%b
	[switch(get(%q0/PRIORITY),
		1,Green,
		2,Yellow,
		3,Red
	)]%b
	%(
	[switch(get(%q0/STATUS),
		0,On Hold,
		1,New,
		2,Underway,
		3,25%%,
		4,50%%,
		5,75%%,
		6,100%%,
		?
	)]
	%)
	%r
	[ljust(
		[rjust(ansi(get(%va/LCARS_COLOR5),Opened On:),10)]%b
		[convsecs(get(%q0/OPENED_ON))],
		div(u(%va/FN_FLEXWIDTH,80),2)
	)]
	[rjust(ansi(get(%va/LCARS_COLOR5),Assigned To:),12)]%b
	[ifelse(
		isdbref(get(%q0/ASSIGNED_TO)),
		name(get(%q0/ASSIGNED_TO)),
		Nobody
	)]
	%r
	[rjust(ansi(get(%va/LCARS_COLOR5),Opened By:),10)]%b%qx
	[ifelse(words(%qy),%r[rjust(ansi(get(%va/LCARS_COLOR5),Tagged:),10)]%b%qy,)]
-

&LCARS_BUCKETHEADER [v(JOB_VA)]=
	[u(%va/FN_HEADER,Bucket List)]
	%r
	[ifelse(
		u(%va/FN_WIZONLY,%#),
		[ansi(get(%va/LCARS_COLOR5),
			[ljust(Name,9)]
			[ljust(Flags,6)]%b%b
			[ljust(Description,u(%va/FN_FLEXWIDTH,30))]
			[rjust(#Rpts,5)]
			[rjust(Pct,5)]
			[space(3)]
			C%b%bA%b%bD%b%bDue
			[space(3)]
			ARTS
		)],
		[ansi(get(%va/LCARS_COLOR5),
			[ljust(NAME,8)]
			[ljust(Description,u(%va/FN_FLEXWIDTH,50))]%b
			[rjust(Viewing,20)]
		)]
	)]
	%r
	[u(%va/FN_BREAK)]
-

&LCARS_BUCKETLIST [v(JOB_VA)]=
	[ifelse(
		u(%va/FN_WIZONLY,%#),
		[ljust(name(%0),6)]%b%b
		[ifelse(
			u(%va/FN_MONITORCHECK,%0,%#),
			V,
			-
		)]
		[ifelse(
			u(%va/IS_HIDDEN,%0),
			H,
			-
		)]
		[ifelse(
			u(%va/IS_LOCKED,%0),
			L,
			-
		)]
		[ifelse(
			u(%va/IS_TAGGED,%0),
			T,
			-
		)]
		[ifelse(
			u(%va/IS_PUBLIC,%0),
			M,
			-
		)]
		[ifelse(
			u(%va/IS_PUBLISHED,%0),
			P,
			-
		)]
		[ifelse(
			u(%va/IS_SUMMARY,%0),
			S,
			-
		)]
		%b%b
		[u(%va/FN_STREXACT,get(%0/DESC),u(%va/FN_FLEXWIDTH,30))]
		[rjust(words(children(%0)),5)]
		[rjust(mul(round(fdiv(words(children(%0)),words(lcon(%va))),2),100)%%,5)]%b%b
		[rjust(get(%0/POST_COMPLETE),2)]%b
		[rjust(get(%0/POST_APPROVE),2)]%b
		[rjust(get(%0/POST_DENY),2)]
		[rjust(default(%0/TURNAROUND,0),5)]
		[rjust(
			[ifelse(
				u(%va/FN_HASATTR,%0,STAT_ART),
				[round(fdiv(fdiv(first(get(%0/STAT_ART)),rest(get(%0/STAT_ART))),86400),0)]d,
				-
			)],
			7
		)],
		[ljust(name(%0),6)]%b%b
		[u(%va/FN_STREXACT,get(%0/DESC),u(%va/FN_FLEXWIDTH,50))]%b
		[rjust(
			ifelse(
				u(%va/FN_MONITORCHECK,%0,%#),
				Yes,
				-
			),
			20
		)]
	)]
-

&LCARS_COLOR1 [v(JOB_VA)]=y
-

&LCARS_COLOR2 [v(JOB_VA)]=hy
-

&LCARS_COLOR3 [v(JOB_VA)]=Yx
-

&LCARS_COLOR4 [v(JOB_VA)]=hw
-

&LCARS_COLOR5 [v(JOB_VA)]=hc
-

&LCARS_FLAGS [v(JOB_VA)]=
	[ifelse(
		u(%va/IS_LOCKED,%0),
		u(%va/FN_FLAG,LOCKED,hr),
	)]
	[ifelse(
		u(%va/IS_PUBLIC,%0),
		u(%va/FN_FLAG,Myjobs),
	)]
	[ifelse(
		u(%va/IS_PUBLISHED,%0),
		u(%va/FN_FLAG,Published),
	)]
-

&LCARS_FOOTER [v(JOB_VA)]=
	[switch(%0,,
		[ansi(get(%va/LCARS_COLOR1),%b[repeat(_,u(%va/FN_FLEXWIDTH,77))])]
		%r
		[ansi(get(%va/LCARS_COLOR1),|[repeat(_,u(%va/FN_FLEXWIDTH,77))]|)],
		[ansi(get(%va/LCARS_COLOR1),%b[repeat(_,u(%va/FN_FLEXWIDTH,77))])]
		%r
		[ansi(get(%va/LCARS_COLOR1),|)]
		[ansi(get(%va/LCARS_COLOR1),rjust(ansi(get(%va/LCARS_COLOR2),stripansi(%0)),u(%va/FN_FLEXWIDTH,77),_))]
		[ansi(get(%va/LCARS_COLOR1),|)]
	)]
-

&LCARS_HEADER [v(JOB_VA)]=
	[ansi(get(%va/LCARS_COLOR1),.-----.)]
	%r
	[ansi(get(%va/LCARS_COLOR1),|[space(5)]|)][ansi(get(%va/LCARS_COLOR4),rjust(%0,u(%va/FN_FLEXWIDTH,70)))]
	%r
	[ansi(get(%va/LCARS_COLOR1),|[space(6)]`-[repeat(_,u(%va/FN_FLEXWIDTH,69))])]
	%r
	[ansi(get(%va/LCARS_COLOR1),`-[repeat(_,u(%va/FN_FLEXWIDTH,76))]|)]
-

&LCARS_JOBLIST [v(JOB_VA)]=
	[setq(j,
		secure(
			ifelse(u(%va/FN_HASATTRP,%1,JOBS),
				lcstr(mid(get(%1/JOBS),0,20)),
				get(%va/JOBS_DEFAULT)
			)
		)
	)]
	[setq(1,
		ifelse(get(%0/DUE_ON),
			ifelse(gt(secs(),get(%0/DUE_ON)),
				r,
				switch(get(%0/PRIORITY),
					1,g,
					2,y,
					3,r,
					g
				)
			),
			switch(get(%0/PRIORITY),
				1,g,
				2,y,
				3,r
			)
		)
	)]
	[ifelse(u(%va/FN_ISNEW,%0,%1),
		[ansi(hr,*)]%b,
		%b%b
	)]
	[ansi(h%q1,
		[rjust([last(name(%0))],5)]%b
		[iter(lnum(strlen(%qj)),
			[u(%va/DISPLAY_[mid(%qj,itext(0),1)],%0,%1)]
		)]
	)]
-

&LCARS_JOBSHEADER [v(JOB_VA)]=
	[u(%va/FN_HEADER,Anomaly Jobs [u(VERSION)])]
	%r
	[setq(j,
		[secure(
			switch(u(%va/FN_HASATTRP,%0,JOBS),
				1,lcstr(mid(get(%0/JOBS),0,20)),
				u(JOBS_DEFAULT)
			)
		)]
	)]
	[setq(2,
		*%b%b
		[ljust(Job#,5)]
		[iter(lnum(strlen(%qj)),
			u(HEADER_[mid(%qj,itext(0),1)])
		)]
	)]
	[ansi(get(%va/LCARS_COLOR3),
		ljust(%q2,u(%va/FN_FLEXWIDTH,79))
	)]
-

&LCARS_READ [v(JOB_VA)]=
	[setq(0,
		ifelse(
			u(%va/EDIT_ACCESS,%#),
				[ansi(n,)]
				[ansi(get(%va/LCARS_COLOR1),%[)]
				[ifelse(
					and(
						or(
							u(%va/IS_PUBLISHED,%1),
							switch(
								extract(get(%1/%0),3,1,|),
									u(%1/OPENED_BY),
									1,
									0
							),
							hasflag(%1/%0,no_inherit)
						),
						u(%va/IS_PUBLIC,%1)
					),
					ansi(get(%va/LCARS_COLOR5),
						[rest(%0,_)]
						+
					),
					ansi(get(%va/LCARS_COLOR5),
						[rest(%0,_)]
						-
					)
				)]
				[ansi(get(%va/LCARS_COLOR1),%])],
		)
	)]
	[setq(1,ansi(get(%va/LCARS_COLOR4),[extract(get(%1/%0),4,1,|)]))]
	[setq(2,
		[ansi(get(%va/LCARS_COLOR1),%[)]
		[ansi(get(%va/LCARS_COLOR4),
			[rest(convsecs(setr(x,extract(get(%1/%0),2,1,|))))]%b
			/%bSD%b
			[u(%va/FN_SECS2SD,extract(get(%1/%0),2,1,|))]
		)]
		[ansi(get(%va/LCARS_COLOR1),%])]
	)]
	[setq(y,sub(u(%va/FN_FLEXWIDTH,64),add(strlen(%q0),strlen(%q1),1)))]
	%b%b%b[ansi(get(%va/LCARS_COLOR1),repeat(_,u(%va/FN_FLEXWIDTH,75)))]
	%r
	[ansi(get(%va/LCARS_COLOR1),.-~)]
	[space(4)]
	[ansi(get(%va/LCARS_COLOR1),repeat(_,6))]
	%q0
	%b
	%q1
	%b
	[ansi(get(%va/LCARS_COLOR1),rjust(%q2,%qy,_))]
	[ansi(get(%va/LCARS_COLOR1),|)]
	%r
	[u(%va/FN_WRAP-L,edit(last(get(%1/%0),|),@@PIPE@@,|),u(%va/FN_FLEXWIDTH,69),ansi(get(%va/LCARS_COLOR1),|[space(5)]|%b%b))]
	%r
	[ansi(get(%va/LCARS_COLOR1),|_____|)]
-

&LCARS_SKIN [v(JOB_VA)]=1
-

&LCARS_STAFFSUM [v(JOB_VA)]=
	%r
	[repeat(-,u(%va/FN_FLEXWIDTH,79))]
	[rjust(ansi(get(%va/LCARS_COLOR5),DB#:),10)]%b
	%0[space(10)]
	[rjust(ansi(get(%va/LCARS_COLOR5),Comments:),10)]%b
	[setr(z,words(lattr(%0/COMMENT_*)))]
	%r%r
	[rjust(ansi(get(%va/LCARS_COLOR5),Players:),10)]%b
	%(Players contributing to this job%)%r
	[setq(y,
		setunion(
			iter(
				lattr(%0/COMMENT_*),
				extract(get(%0/##),4,1,|),%b,|
			),,|
		)
	)]
	[u(%va/fn_columns,%qy,20,|,11)]
	[ifelse(
		u(%va/fn_hasattr,%0,LIST_READERS),
		%r[rjust(ansi(get(%va/LCARS_COLOR5),Readers:),10)]%b
			%(Players who have read this job in the past%)%r
		[setq(z,
			iter(
				get(%0/LIST_READERS),
				first(##,|)
			)
		)]
		[u(%va/fn_columns,map(%va/MAP_NAME,%qz,%b,|),20,|,11)],
	)]
	%r
	[rjust(ansi(get(%va/LCARS_COLOR5),Stats:),10)]%r
	[u(%va/fn_columns,
		iter(
			ifelse(
				u(%va/fn_hasattr,%0,LIST_STATS),
				sort(get(%0/LIST_STATS)),
			),
			[first(##,|)]%b[last(##,|)],%b,|
		),20,|,11
	)]%r
-

&LCARS_SUMMARY [v(JOB_VA)]=
	%r
	[u(%va/LCARS_FOOTER,SUMMARY)]
	%r
	[u(parent(%0)/SUMMARY,%0)]
-

&FN_SECS2SD [v(JOB_VA)]=
	[fdiv(
		div(
			add(%0,862760000),
			3600
		),
		10
	)]
-

&FN_WRAP-L [v(JOB_VA)]=
	switch(first(version()),PennMUSH,
		%2[wrap(%0,%1,%1,%R%2)],
		wrap(%0,%1,l,%2)
	)
-
