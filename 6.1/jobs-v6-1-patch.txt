@@
@@ Anomaly Jobs 6.1 Patch Installer
@@ For upgrades from 6.0. To upgrade earlier versions, use the full installer.
@@ Quote this file from your client with a 1-second delay
@@
think [repeat(=,77)]%r%r[center(ansi(hc,Setting up Anomaly Jobs v6.1),77)]%r%r[repeat(=,77)]
@set me=quiet
think [ansi(hc,ANOMALY JOBS:)] Checking current installation.
@switch [ifelse(isdbref(setr(0,switch(first(version()),PennMUSH,lsearch(all,eobjects,\[strmatch(name(##),Job Global Object <JGO>)\]),RhostMUSH,searchng(object=Job Global Object <JGO>),search(object=Job Global Object <JGO>)))),setq(1,get(%q0/VA)),[setq(1,switch(first(version()),PennMUSH,lsearch(all,eobjects,\[strmatch(name(##),Job Database <JD>)\]),RhostMUSH,searchng(object=Job Database <JD>),search(object=Job Database <JD>)))][setq(0,loc(%q1))])][isdbref(%q0)][and(isdbref(%q1),gte(edit(first(default(%q1/version,0),.),v,),6))]=0*,{think [ansi(hc,ANOMALY JOBS:)] No previous jobs installation found.  Use the full install.;think [ansi(hc,ANOMALY JOBS:)] [ansi(hr,Aborting. Cancel this script in your client.)];&JOB_GO %#=#-1;&JOB_VA %#=#-1;&JOB_VB %#=#-1;&JOB_VC %#=#-1;&JOB_PATCH %#=1},10*,{think [ansi(hc,ANOMALY JOBS:)] [ansi(hr,Not a valid Jobs 6.0 installation. Use the full install.)];think [ansi(hc,ANOMALY JOBS:)] [ansi(hr,Aborting. Cancel this script in your client.)];&JOB_GO %#=#-1;&JOB_VA %#=#-1;&JOB_VB %#=#-1;&JOB_VC %#=#-1;&JOB_PATCH %#=1},{think [ansi(hc,ANOMALY JOBS:)] Current installation found.  Updating.;&JOB_GO %#=%q0;&JOB_VA %#=%q1;&JOB_VB %#=get(%q0/VB);&JOB_VC %#=get(%q0/VC);&JOB_PATCH %#=1}
think [ansi(hc,ANOMALY JOBS:)] Setting up commands.
&CMD_JOB/DENY [v(JOB_GO)]=$+job/deny *=*:@switch [u(%va/HAS_ACCESS,%#)][or(u(%va/DENY_ACCESS,%#),u(%va/FN_STAFFALL,%#))][setq(0,u(%va/FN_FIND-JOB,%0))][isdbref(%q0)][u(%va/FN_ACCESSCHECK,parent(%q0),%#)][not(u(%va/IS_LOCKED,%q0,%#))][udefault(%q0/DNY_ACCESS,1,%#)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=Permission denied.},110*,{@pemit %#=There is no job by that number.},1110*,{@pemit %#=You cannot access that job.},11110*,{@pemit %#=That job is presently [ifelse(u(%va/FN_HASATTRP,%q0,CHECKOUT),checked out to [name(first(get(%q0/CHECKOUT)))],locked)]},111110*,{@pemit %#=That action is not available for that job.},{@pemit %#=[u(%va/FN_ADDSTAT_ART,parent(%q0),%q0)][u(%va/FN_ADDSTAT_ART,%vc,%q0)]You have denied job #%0.;@trigger %va/TRIG_BROADCAST=%q0,%#,DNY;@trigger %va/TRIG_ADD=%q0,[u(%va/FN_STRTRUNC,%1,get(%va/BUFFER))],%#,DNY;@trigger %va/TRIG_DESTROY=%q0}
&CMD_JOB/DUE [v(JOB_GO)]=$+job/due *=*:@switch [not(eq(words(%1,/),3))][u(%va/HAS_ACCESS,%#)][setq(0,u(%va/FN_FIND-JOB,%0))][isdbref(%q0)][u(%va/FN_ACCESSCHECK,parent(%q0),%#)][udefault(%q0/DUE_ACCESS,1,%#)][switch(lcstr(%1),none,2,ifelse(gt(setr(1,u(%va/FN_STRINGSECS,%1)),0),1[setq(1,add(%q1,secs()))],ifelse(gt(setr(1,convtime(%1)),0),1,ifelse(gt(setr(1,convtime(XXX %1)),0),1,t(gt(setr(1,convtime(%1 23:59:59)),0))))))]=0*,{@@},10*,{@pemit %#=Permission denied.},110*,{@pemit %#=That is not a valid job number.},1110*,{@pemit %#=You do not have access to that job.},11110*,{@pemit %#=That action is not available for that job.},111110*,{@pemit %#=Invalid date. Use date formatting as '[rest(time())]'.},111112*,{@pemit %#=You have cleared Job Number %0's date.;&DUE_ON %q0=;@trigger %va/TRIG_ADD=%q0,Due date cleared.,%#,DUE;@trigger %va/TRIG_BROADCAST=%q0,%#,DUE},{@pemit %#=You have set Job Number %0's date to %1.;&DUE_ON %q0=%q1;@trigger %va/TRIG_ADD=%q0,Due on [secure(%1)].,%#,DUE,[secure(%1)];@trigger %va/TRIG_BROADCAST=%q0,%#,DUE}
&CMD_JOB/DUE2 [v(JOB_GO)]=$+job/due *=*/*/*:@switch [u(%va/HAS_ACCESS,%#)][isdbref(setr(0,u(%va/FN_FIND-JOB,%0)))][setq(1,convtime(XXX [switch([rjust(%1,2,0)],01,Jan,02,Feb,03,Mar,04,Apr,05,May,06,Jun,07,Jul,08,Aug,09,Sep,10,Oct,11,Nov,12,Dec)] [rjust(%2,2,0)] 23:59:00 [switch(strlen(%3),2,20[rjust(%3,2,0)],%3)]))][gt(%q1,0)][u(%va/FN_ACCESSCHECK,parent(%q0),%#)][udefault(%q0/DUE_ACCESS,1,%#)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=That is not a valid job number.},110*,{@pemit %#=Invalid date. Use date formatting as 'MM/DD/YY', '[rest(time())]', 'none' to clear.},1110*,{@pemit %#=You do not have access to that job.},11110*,{@pemit %#=That action is not available for that job.},{@pemit %#=You have set Job Number %0's date to [convsecs(%q1)].;&DUE_ON %q0=%q1;@trigger %va/TRIG_ADD=%q0,Due on [convsecs(%q1)].,%#,DUE;@trigger %va/TRIG_BROADCAST=%q0,%#,DUE}
&CMD_JOB/LAST [v(JOB_GO)]=$+job/last *=*:@switch [u(%va/HAS_ACCESS,%#)][setq(0,u(%va/FN_FIND-JOB,%0))][isdbref(%q0)][u(%va/FN_ACCESSCHECK,parent(%q0),%#)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There is no job by that name. Please type '+jobs/all' for all jobs or '+jobs/<type>' to list by type.},110*,{@pemit %#=You do not have access to that job.},{@pemit %#=[u(%va/FN_READERS,%q0,%#)][u(%va/FN_HEADER,View [name(%q0)])]%r[ulocal(%va/FN_BANNER,%q0)];@dolist [extract(setr(1,filter(%va/FIL_COMMENTS,sortby(%va/SORTBY_COMMENTS,lattr(%q0/COMMENT_*)))),ifelse(lt(sub(words(%q1),%1),1),1,inc(sub(words(%q1),%1))),inc(%1))]={@pemit %#=[u(%va/FN_READJOB,##,%q0)]};@wait 0={@pemit %#=[u(%va/FN_FOOTER,u(%va/FN_FLAGS,%q0))]};}
&CMD_JOB/UNDELETE [v(JOB_GO)]=$+job/undelete *:@switch [u(%va/HAS_ACCESS,%#)][setq(0,u(%va/FN_FIND-JOB,%0))][isdbref(%q0)][u(%va/FN_ACCESSCHECK,parent(%q0),%#)][u(%va/FN_HASATTR,%q0,GOING)][udefault(%q0/UND_ACCESS,1,%#)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There is no job by that number.},110*,{@pemit %#=You do not have access to that job.},1110*,{@pemit %#=That job has not been deleted.},11110*,{@pemit %#=That action is not available for that job.},{@pemit %#=You have restored job %0.;@set %q0=!going;&going %q0;@trigger %va/TRIG_ADD=%q0,Undeleted job.,%#,UND;@trigger %va/TRIG_BROADCAST=%q0,%#,UND,%q1}
&CMD_JOBS/DATE [v(JOB_GO)]=$+jobs/date:@fo %#=+jobs/select all sort=date
&CMD_JOBS/DUE [v(JOB_GO)]=$+jobs/due:@fo %#=+jobs/select all sort=due
&CMD_JOBS/FROM [v(JOB_GO)]=$+jobs/from *:@fo %#=+jobs/select source=%0
&CMD_JOBS/LIST [v(JOB_GO)]=$+jobs/list *:@switch [u(%va/HAS_ACCESS,%#)][setq(0,u(%va/FN_FIND-BUCKET,%0))][isdbref(%q0)][u(%va/FN_ACCESSCHECK,%q0,%#)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=That is not a valid bucket. Type '[ansi(h,+buckets)]' for a list of valid buckets.},110*,{@pemit %#=You do not have access to that bucket.},{@pemit %#=[u(%va/FN_JOBSHEADER,%#)];@dolist [sortby(%va/SORTBY_JOBNUM,remove(children(%q0),#-1))]={@pemit %#=[u(%va/FN_JOBLIST,##,%#)]};@wait 0=@pemit %#=[u(%va/FN_FOOTER,* Denotes New Activity)];}
&CMD_JOBS/PRI [v(JOB_GO)]=$+jobs/pri:@fo %#=+jobs/select all sort=pri
&CMD_JOBS/SELECT [v(JOB_GO)]=$+jobs/select *:@switch [u(%va/HAS_ACCESS,%#)][setq(0,before(lcstr(%0),sort=))][setq(0,u(%va/FN_INFIX2POSTFIX,map(%va/MAP_JOBSELECT,u(%va/FN_TOKENLIST,%q0))))][not(strmatch(%q0,*#-1 MISMATCHED PARENS*))][not(strmatch(%q0,*#-1 SYNTAX ERROR*))][setq(9,u(%va/FN_TRIM,after(lcstr(%0),sort=)))][ifelse(setr(8,strmatch(%q9,-*)),setq(9,mid(%q9,1,dec(strlen(%q9)))),)][setq(9,ifelse(strlen(%q9),%q9,jobnum))][u(%va/FN_HASATTRP,%va,SORTBY_%q9)]=0*,@pemit %#=Permission denied.,10*,@pemit %#=Mismatched parentheses.,110*,@pemit %#=Syntax error.,1110,@pemit %#=Invalid sort type.,{@pemit %#=[u(%va/FN_JOBSHEADER,%#)];@dolist [setq(1,filter(%va/FIL_GOING,lcon(%va)))][u(%va/FN_S-INIT)][map(%va/MAP_PARSESTACK,%q0)][ifelse(%q8,revwords(sortby(%va/SORTBY_%q9,u(%va/FN_S-PEEK))),sortby(%va/SORTBY_%q9,u(%va/FN_S-PEEK)))]={@pemit %#=[ifelse(or(u(%va/FN_ACCESSCHECK,parent(##),%#),u(%va/FIL_MINE,##)),u(%va/FN_JOBLIST,##,%#),)];};@wait 0=@pemit %#=[u(%va/FN_FOOTER,* Denotes New Activity)]}
&CMD_JOBS/SORT [v(JOB_GO)]=$+jobs/sort:@fo %#=+jobs/select all sort=bucket
&CMD_MYJOB/VIEW [v(JOB_GO)]=$+myjob *:@switch [not(haspower(%#,guest))][setq(0,u(%va/FN_FIND-JOB,%0))][isdbref(%q0)][or(gt(member(get(%q0/TAGGED_FOR),%#),0),u(%va/IS_TRANSPARENT,%q0),and(get(%q0/PUBLIC),match(get(%q0/OPENED_BY),%#)))]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=That is not a valid job number.},110*,{@pemit %#=Job %0 is not yours. You can only view your own jobs.},{@pemit %#=[u(%va/FN_READERS,%q0,%#)][u(%va/FN_HEADER,View [name(%q0)])]%r[u(%va/FN_MYBANNER,%q0)][ifelse(u(%va/FN_HASATTRP,%q0,MYSUMMARY),[ulocal(%va/FN_MYSUMMARY,%q0,%#)],)];@dolist [filter(%va/FIL_PUBLISHED,filter(%va/FIL_COMMENTS,sortby(%va/SORTBY_COMMENTS,lattr(%q0/COMMENT_*))))]={@pemit %#=[u(%va/FN_READJOB,##,%q0)]};@wait 0=@pemit %#=[u(%va/FN_FOOTER)]}
&CMD_MYJOB/LAST [v(JOB_GO)]=$+myjob/last *=*:@switch [not(haspower(%#,guest))][setq(0,u(%va/FN_FIND-JOB,%0))][isdbref(%q0)][or(gt(member(get(%q0/TAGGED_FOR),%#),0),u(%va/IS_TRANSPARENT,%q0),and(get(%q0/PUBLIC),match(get(%q0/OPENED_BY),%#)))]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=That is not a valid job number.},110*,{@pemit %#=Job %0 is not yours. You can only view your own jobs.},{@pemit %#=[u(%va/FN_READERS,%q0,%#)][u(%va/FN_HEADER,View [name(%q0)])]%r[u(%va/FN_MYBANNER,%q0)][ifelse(u(%va/FN_HASATTRP,%q0,MYSUMMARY),[ulocal(%va/FN_MYSUMMARY,%q0,%#)],)];@dolist [extract(setr(1,filter(%va/FIL_PUBLISHED,filter(%va/FIL_COMMENTS,sortby(%va/SORTBY_COMMENTS,lattr(%q0/COMMENT_*))))),ifelse(lt(sub(words(%q1),%1),1),1,inc(sub(words(%q1),%1))),inc(%1))]={@pemit %#=[u(%va/FN_READJOB,##,%q0)]};@wait 0=@pemit %#=[u(%va/FN_FOOTER)]}
think [ansi(hc,ANOMALY JOBS:)] Setting controlling attributes.
@switch [v(JOB_PATCH)]=1,{@dolist STARTUP DATA_CHARS2 DISPLAY_MYHEADERS DISPLAY_MYJOB MYJOBS_HEADER FIL_DELETE FN_ANSIFY FN_EASYPIE_PENN FN_EASYPIE_TINY PENN_PIE PENN10_PIE PENN12_PIE FN_NEW_JOBS FN_PACK IS_PLAYER JOBS_HEADER JOBS_HEADER2 MAP_ANSIFY MAP_ARTSGRAPH MAP_NAME2 MAP_NEW SORTBY_JOB SORTBY_SORT={@switch [hasattr(v(JOB_VA),##)]=1,&## [v(JOB_VA)]}}
&FIL_DUEAFTER [v(JOB_VA)]=or(not(strlen(get(%0/DUE_ON))),eq(get(%0/DUE_ON),0),gt(get(%0/DUE_ON),%1))
&FIL_PUBLISHED [v(JOB_VA)]=or(u(%q0/PUBLISH),hasflag(%q0/%0,no_inherit),member(get(%q0/TAGGED_FOR),%#),strmatch(extract(get(%q0/%0),3,1,|),%#))
&FN_BARGRAPH [v(JOB_VA)]=[setq(4,ifelse(%5,%5,%B))][setq(5,ifelse(%2,%2,2))][setq(6,ifelse(%3,ifelse(gt(%3,16),16,%3),1))][setq(7,switch(%4,,#,%4))][setq(1,ifelse(%1,%1,18))][setq(2,%q1)][setq(0,last(sort(%0,n,%q4,%q4),%q4))][setq(3,fdiv(%q0,%q1))][setq(x,iter(lnum(%q1),[setq(9,0)][switch(%q2,%q1,rjust(u(FN_BARGRAPHAXIS,%q0),5),ifelse(mod(%q1,2),inc(div(%q1,2)),div(%q1,2)),rjust(u(FN_BARGRAPHAXIS,div(%q0,2)),5),[space(5)])]|[map(MAP_GRAPH,%0,%q4,%b)][setq(2,sub(%q2,1))],%B,%R))]%qx%R[space(4)]0+[repeat(-,dec(add(mul(words(%0,%q4),%q5),words(%0,%q4))))]
&FN_BARGRAPHAXIS [v(JOB_VA)]=switch(log(%0),<5,%0,<6,[u(%va/FN_TRIM,u(%va/FN_STRTRUNC,fdiv(%0,1E3),4),r,.)]K,<9,[u(%va/FN_TRIM,u(%va/FN_STRTRUNC,fdiv(%0,1E6),4),r,.)]M,[u(%va/FN_TRIM,u(%va/FN_STRTRUNC,fdiv(%0,1E9),4),r,.)]B)
&FN_INFIX2POSTFIX [v(JOB_VA)]=[u(%va/FN_S-INIT)][setq(v,map(%va/MAP_PARAMS,lattr(%va/SEL_*)))][u(%va/FN_TRIM,squish([map(%va/MAP_INFIX2POSTFIX,%0)]%B[u(%va/FN_S-POP-BINARY)][switch(1,t(member(%( %),u(%va/FN_S-PEEK))),#-1 MISMATCHED PARENS)]))]
&FN_JGROUPMEMBERS [v(JOB_VA)]=switch(first(version()),PennMUSH,lsearch(all,eplayers,\[u(%0/ISMEMBER,##)\]),RhostMUSH,searchng(eplayer=u(%0/ISMEMBER,##)),search(eplayer=u(%0/ISMEMBER,##)))
&FN_PCT [v(JOB_VA)]=mul(round(ifelse(gt(get(%va/MAX_JOBS),0),max(fdiv(words(remove(lcon(%va),#-1)),get(%va/MAX_JOBS)),fdiv(strlen(lcon(%va)),get(%va/BUFFER))),fdiv(strlen(lcon(%va)),get(%va/BUFFER))),2),100)
&FN_STRINGSECS [v(JOB_VA)]=switch(first(version()),PennMUSH,stringsecs(%0),ladd(map(MAP_ETIME,%0)))
&LIST_ACTIONS [v(JOB_VA)]=ADD Player input|ASN Assignment|CKI Check In|CKO Check Out|CLN Clone|CRE Create|DUE Due date|EDT Edited job|LOK Locked Job|MRG Merged job|NAM Re-name job|PUB Publication|SRC Source change|STA Status change|SUM Sumset change|TAG Job tag|TRN Transfer|UND Undelete job|UNL Unlock job|UNP Unpublish|COM +job/complete|DNY +job/deny|APR +job/approve|DEL +job/delete
&MAP_ETIME [v(JOB_VA)]=switch(%0,*d,mul(86400,before(%0,d)),*h,mul(3600,before(%0,h)),*m,mul(60,before(%0,m)),*s,before(%0,s),0)
&MAP_GRAPH [v(JOB_VA)]=[setq(9,ifelse(gt(inc(%q9),%q6),1,inc(%q9)))][ifelse(lte(%q2,add(fdiv(%0,%q3),0.5)),ansi(v(COLORGRAPH_%q9),repeat(%q7,%q5)),repeat(%b,%q5))]
&MAP_JOBSELECT [v(JOB_VA)]=switch(1,t(match(and or not %( %) | & !,%0)),%0,u(%va/FN_HASATTR,%#,JOBSELECT_%0),get(%#/JOBSELECT_%0),%0)
think [ansi(hc,ANOMALY JOBS:)] Installing reports.
&REPORT_ACTBY [v(JOB_VA)]=[switch(%0,,This report requires you to supply an action code.%r%rUse: '+jobs/reports ACTBY=<action code>'.%r,[setq(0,squish(iter(lcon(%vc),ifelse(eq(words(grab(u(##/LIST_STATS),%0|*)),0),,##))))][setq(1,squish(iter(%q0,rest(grab(u(##/LIST_STATS),%0|*),|))))][ifelse(lt(words(%q1),1),There is no action code '[ucstr(%0)]' presently in the system.%r,[ulocal(FN_BARGRAPH,%q1,10,2,16,#)]%r[space(6)][iter(%q0,[u(%va/FN_STREXACT,name(##),2)])]%r%r[center([ucstr(%0)] Actions by Bucket,u(%va/FN_FLEXWIDTH,79))])])]
&REPORT_ACTIONS [v(JOB_VA)]=[ulocal(FN_BARGRAPH,setr(0,map(MAP_ACTIONS,v(LIST_STATS))),15,2,16,#)][setq(0,fold(FOLD_ADD,%q0,0))]%r[space(6)][setq(1,map(MAP_ACTIONS2,sort(v(LIST_STATS))))][iter(%q1,[mid(##,0,1)]%b)]%r[space(6)][iter(%q1,[mid(##,1,1)]%b)]%r[space(6)][iter(%q1,[mid(##,2,1)]%b)]%r%r[center(%q0 Actions Performed Since [v(INSTALL_DATE)],u(%va/FN_FLEXWIDTH,79))]
&REPORT_ARTSGRAPH [v(JOB_VA)]=[setq(0,iter(setr(1,sortby(%va/SORTBY_NAME,lcon(%vc))),ifelse(u(%va/FN_HASATTR,##,STAT_ART),[div(round(fdiv(first(get(##/STAT_ART)),rest(get(##/STAT_ART))),0),86400)],0)))][ulocal(%va/FN_BARGRAPH,%q0,15,2,16,#)]%r[space(6)][iter(%q1,mid([name(##)]%B,0,2))]%r%r[center(Average Resolution Time by Bucket %(in days%),u(%va/FN_FLEXWIDTH,79))]
&REPORT_BACT [v(JOB_VA)]=[switch(%0,,This report requires you to supply a bucket name.%r%rUse: '+jobs/reports BACT=<bucket name>' to specify a bucket.%r,[setq(0,u(%va/FN_FIND-BUCKET,%0))][ifelse(isdbref(%q0),[ulocal(FN_BARGRAPH,map(MAP_ACTIONS,get(%q0/LIST_STATS)),15,1,16,#)]%r[space(6)][setq(1,map(MAP_ACTIONS2,sort(get(%q0/LIST_STATS))))][iter(%q1,mid(##,0,1))]%r[space(6)][iter(%q1,mid(##,1,1))]%r[space(6)][iter(%q1,mid(##,2,1))]%r%r[center(Number of Actions Performed on Bucket [name(%q0)],u(%va/FN_FLEXWIDTH,79))],That is not a valid bucket name. See '+buckets' for a list of valid buckets.)])]
&REPORT_BUCKETS [v(JOB_VA)]=[u(FN_BARGRAPH,iter(sortby(SORTBY_NAME,lcon(%vc)),words(remove(children(##),#-1))),15,2,16,#)]%r[space(6)][iter(sortby(SORTBY_NAME,lcon(%vc)),mid([name(##)]%b,0,2))]%r%r[center(# of Jobs in Buckets,u(%va/FN_FLEXWIDTH,79))]
&REPORT_JACT [v(JOB_VA)]=[switch(%0,,This report requires you to supply a job.%r%rUse: +jobs/reports JACT=<#>%r,[setq(0,u(%va/FN_FIND-JOB,%0))][ifelse(isdbref(%q0),[ifelse(u(%va/FN_HASATTR,%q0,LIST_STATS),[ulocal(FN_BARGRAPH,map(MAP_ACTIONS,get(%q0/LIST_STATS)),15,1,16,#)]%r[space(6)][setq(1,map(MAP_ACTIONS2,sort(get(%q0/LIST_STATS))))][iter(%q1,mid(##,0,1))]%r[space(6)][iter(%q1,mid(##,1,1))]%r[space(6)][iter(%q1,mid(##,2,1))],No actions recorded for this job.)]%r%r[center(Actions Performed on [name(%q0)],u(%va/FN_FLEXWIDTH,79))],That is not a valid job.%r)])]
&REPORT_STATS [v(JOB_VA)]=[rjust(ansi(hc,Install:),20)]%b[get(%va/INSTALL_DATE)]%r[rjust(ansi(hc,Version:),20)]%bAnomaly Jobs [get(%va/VERSION)][space(5)]%r[rjust(ansi(hc,Release:),20)]%b[get(%va/RELEASE)]%r[rjust(ansi(hc,Memory:),20)]%b[setr(a,fold(%va/FOLD_ADD,iter(setr(b,[num(me)] [loc(me)] %vb %vc [lcon(%vc)] [lcon(me)]),objmem(##))))]%b%([round(fdiv(%qa,1024),1)]k in [words(%qb)] objects%)%r[rjust(ansi(hc,Open Jobs:),20)]%b[words(remove(lcon(%va),#-1))]%r[rjust(ansi(hc,Capacity:),20)]%b[u(%va/FN_PCT)]%% Full%r[rjust(ansi(hc,Next Job:),20)]%b[inc(u(%va/JOBS_NUM))]%r[rjust(ansi(hc,Buckets:),20)]%b[words(lcon(%vc))]%r[rjust(ansi(hc,Opened:),20)]%b[last(grab(get(%va/LIST_STATS),CRE|*),|)][setq(x,iter(APR DNY COM DEL,grab(get(%va/LIST_STATS),##|*)))][setq(y,map(%va/MAP_ACTIONS,%qx))]%r[rjust(ansi(hc,Closed:),20)]%b[fold(%va/FOLD_ADD,%qy,0)]%r[rjust(ansi(hc,Actions To Date:),20)]%b[fold(FOLD_ADD,map(MAP_ACTIONS,v(LIST_STATS)),0)]%r[rjust(ansi(hc,ART:),20)]%b[u(%va/FN_ART,%vc)][space(10)] ART=Average Resolution Time%r[rjust(ansi(hc,REQ ART:),20)]%b[u(%va/FN_ART,locate(%vc,REQ,i))][space(18)]DD:HH:MM:SS%r[rjust(ansi(hc,CODE ART:),20)]%b[u(%va/FN_ART,locate(%vc,CODE,i))]%r[rjust(ansi(hc,TPS ART:),20)]%b[u(%va/FN_ART,locate(%vc,TPS,i))]%r[rjust(ansi(hc,BUILD ART:),20)]%b[u(%va/FN_ART,locate(%vc,BUILD,i))]%r
&SORTBY_BUCKET [v(JOB_VA)]=comp(name(parent(%0)),name(parent(%1)))
&SORTBY_DATE [v(JOB_VA)]=sub(get(%0/MODIFIED_ON),get(%1/MODIFIED_ON))
&SORTBY_DUE [v(JOB_VA)]=comp(get(%0/DUE_ON),get(%1/DUE_ON))
&SORTBY_PRI [v(JOB_VA)]=sub(get(%0/PRIORITY),get(%1/PRIORITY))
&TRIG_ADD [v(JOB_VA)]=think [setq(0,default(%0/NUM_COMMENT,1))][setq(1,last(grab(get(%0/LIST_READERS),%2|*),|))][setq(2,edit(%1,|,@@PIPE@@))][ifelse(eq(dec(%q0),%q1),set(%0,LIST_READERS:[setunion(remove(get(%0/LIST_READERS),%2|%q1),%2|%q0)]),)][ulocal(FN_ADDSTAT,%3,%va)][ulocal(FN_ADDSTAT,%3,%0)][ulocal(FN_ADDSTAT,%3,parent(%0))];&COMMENT_%q0 %0=%3|[secs()]|%2|[name(%2)]|[ifelse(u(%va/FN_HASATTRP,parent(%0),ALETTER_%3),u(%va/FN_STRTRUNC,u([parent(%0)]/ALETTER_%3,%0,%2,%q2),get(%va/BUFFER)),u(%va/FN_STRTRUNC,%q2,get(%va/BUFFER)))];&NUM_COMMENT %0=[inc(%q0)];&MODIFIED_ON %0=[secs()];&MODIFIED_BY %0=%2;@trigger parent(%0)/HOOK_%3=%0,%2,[parent(%0)],%1;@trigger %va/TRIG_POST=%0,%3,%2,%1;@trigger %va/TRIG_MAIL=%0,%3,%2,%1
&TRIG_CREATE [v(JOB_VA)]=@switch gte(u(%va/FN_PCT),100)=1,{@pemit %0=[ansi(hr,JOBS ERROR:)] Creation failed. The +jobs system is full. Contact staff immediately.;@trigger %va/TRIG_BROADCAST=%1,JOBS SYSTEM IS FULL! Please complete jobs or boost your MAX_JOBS configuration.},{think [setq(0,inc(u(%va/JOBS_NUM)))][setq(1,create(Job %q0,10))][setq(y,case(%7,1,CRE,OTH))];@tel %q1=%va;@link %q1=%va;&OPENED_BY %q1=[switch(%5,,%0,%5)];&OPENED_ON %q1=[secs()];&MODIFIED_ON %q1=[secs()];&STATUS %q1=1;&TITLE %q1=%3;@parent %q1=[locate(%vc,%1,i)];&NUM_COMMENT %q1=2;&PRIORITY %q1=%2;&COMMENT_1 %q1=CRE|[secs()]|%0|[name(%0)]|[ifelse(u(%va/FN_HASATTRP,parent(%q1),ALETTER_%qy),u(%va/FN_STRTRUNC,u([parent(%q1)]/ALETTER_%qy,%q1,%0,edit(%4,|,@@PIPE@@)),get(%va/BUFFER)),u(%va/FN_STRTRUNC,edit(%4,|,@@PIPE@@),get(%va/BUFFER)))];&LIST_READERS %q1=%0|1;&ASSIGNED_TO %q1=%6;&JOBS_NUM %va=%q0;&DUE_ON %q1=[ifelse(u(%va/FN_HASATTRP,%q1,TURNAROUND),add(secs(),mul(60,60,get(%q1/TURNAROUND))),)];@trigger %va/TRIG_BROADCAST=%q1,%0,%qy,%3;@trigger %1/HOOK_%qy=%q1,%0,%1,%4;@trigger %va/TRIG_POST=%q1,%qy,%0,%4;@trigger %va/TRIG_MAIL=%q1,%qy,%0,%4;think [ulocal(FN_ADDSTAT,CRE,%va)][ulocal(FN_ADDSTAT,CRE,parent(%q1))];@switch gt(u(%va/FN_PCT),95)=1,{@trigger %va/TRIG_BROADCAST=%1,JOBS SYSTEM IS NEARING CAPACITY! Please complete jobs or boost your MAX_JOBS configuration.}}
&RELEASE [v(JOB_VA)]=August 21, 2010
&VERSION [v(JOB_VA)]=v6.1
&VERSION [v(JOB_VC)]=v6.1
think [ansi(hc,ANOMALY JOBS:)] Setting up master parent.
&BLETTER_UND [v(JOB_VC)]=[name(%0)] has been undeleted by [name(%1)].
@switch [hasattr(v(JOB_VC),STAT_ART)]=0,{&STAT_ART [v(JOB_VC)]=0 0}
@set [v(JOB_VC)]/STAT_ART=NO_INHERIT
@wait 10={think [ansi(hc,ANOMALY JOBS:)] Script complete. Thanks for using Anomaly Jobs!;@set me=!quiet}
