#
# Temp for test environment. Need to make part of install
#

@vd [v(JOB_VA)]=#131
-

@parent WHITEBG=#131
-

#
# Test version of this invokes the %VD version for invalid skin name
# substitute dbref of WHITEBG skin for %vd to test it
#

&FN_SKIN [v(JOB_VA)]=
	[setq(z,default(%#/JOBSKIN,DEFAULT))]
	[ifelse(
		and(u(%va/FN_HASATTR,me,%qz_SKIN),u(%va/FN_HASATTR,me,%qz_%0)),
		u(%qz_%0,%1,%2,%3,%4),
		u(%vd/%0,%1,%2,%3,%4)
	)]
-

#
# Check caller for JOBSKIN_* attribute. If not use skin's *.
#

&FN_STYLE [v(JOB_VD)]=
	default(%#/JOBSKIN_%0,v(%0))
-

#
# The color styles for DEFAULT
#

&COLOR_BANNER [v(JOB_VD)]=hc
-

&COLOR_BUCKETHEADER [v(JOB_VD)]=hc
-

&COLOR_FOOTERTEXT [v(JOB_VD)]=n
-

&COLOR_HEADERTEXT [v(JOB_VD)]=hy
-

&COLOR_NEWJOBMARKER [v(JOB_VD)]=hr
-

&COLOR_JOBSHEADER [v(JOB_VD)]=hc
-

&COLOR_JOBRED [v(JOB_VD)]=hr
-

&COLOR_JOBYELLOW [v(JOB_VD)]=hy
-

&COLOR_JOBGREEN [v(JOB_VD)]=hg
-

&COLOR_READJOBNUM [v(JOB_VD)]=hc
-

&COLOR_READJOBSTAMP [v(JOB_VD)]=h
-

#
# The old &DEFAULT_* attributes
#

&BANNER [v(JOB_VD)]=
	[setq(0,%0)]
	[setq(x,
		u(%va/FN_ITEMIZE,
			map(%va/MAP_NAME,get(%q0/OPENED_BY),%b,|),
		|)
	)]
	[setq(y,
		u(%va/FN_ITEMIZE,
			map(%va/MAP_NAME,get(%q0/TAGGED_FOR),%b,|),
			|
		)
	)]
	[ljust(
		[rjust(
			ansi(u(me/FN_STYLE,COLOR_BANNER),Bucket:),
			10
		)]%b
		[u(%va/FN_BUCKETNAME,%q0)],
		div(u(%va/FN_FLEXWIDTH,80),2)
	)]
	[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Due On:),12)]%b
	[ifelse(
		get(%q0/DUE_ON),
		ifelse(
			gt(secs(),get(%q0/DUE_ON)),
			OVERDUE!,
			convsecs(get(%q0/DUE_ON))
		),
		-
	)]%r
	[ljust(
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Title:),10)]%b
		[get(%q0/TITLE)]
		,div(u(%va/FN_FLEXWIDTH,80),2)
	)]
	[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Status:),12)]%b
	[switch(
		get(%q0/PRIORITY),
		1,Green,
		2,Yellow,
		3,Red
	)]%b%(
	[switch(
		get(%q0/STATUS),
		0,On Hold,
		1,New,
		2,Underway,
		3,25%%,
		4,50%%,
		5,75%%,
		6,100%%,
		?
	)]%)%r
	[ljust(
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Opened On:),10)] [convsecs(get(%q0/OPENED_ON))],
		div(u(%va/FN_FLEXWIDTH,80),2)
	)]
	[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Assigned To:),12)]%b
	[ifelse(
		isdbref(get(%q0/ASSIGNED_TO)),
		name(get(%q0/ASSIGNED_TO)),
		Nobody
	)]%r
	[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Opened By:),10)]%b%qx
	[ifelse(words(%qy),%r[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Tagged:),10)]%b%qy,)]
-

&BREAK [v(JOB_VD)]=
	switch(
		%0,
		,repeat(-,u(%va/FN_FLEXWIDTH,79)),
		rjust(%[%0%],u(%va/FN_FLEXWIDTH,79),-)
	)
-

&BUCKETHEADER [v(JOB_VD)]=
	[u(%va/FN_HEADER,Bucket List)]%r
	[ifelse(
		u(%va/FN_WIZONLY,%#),
		[ansi(u(me/FN_STYLE,COLOR_BUCKETHEADER),
			[ljust(Name,9)]
			[ljust(Flags,6)]%b%b
			[ljust(Description,u(%va/FN_FLEXWIDTH,30))]
			[rjust(#Jobs,5)]
			[rjust(Pct,5)]
			[space(3)]C%b%bA%b%bD%b%bDue
			[space(3)]
			ARTS
		)],
		[ansi(u(me/FN_STYLE,COLOR_BUCKETHEADER),
			[ljust(NAME,8)]
			[ljust(Description,u(%va/FN_FLEXWIDTH,50))]%b
			[rjust(Viewing,20)]
		)]
	)]%r
	[u(%va/FN_BREAK)]
-

&BUCKETLIST [v(JOB_VD)]=
	[ifelse(
		u(%va/FN_WIZONLY,%#),
		[ljust(name(%0),6)]%b%b
			[ifelse(u(%va/FN_MONITORCHECK,%0,%#),V,-)]
			[ifelse(u(%va/IS_HIDDEN,%0),H,-)]
			[ifelse(u(%va/IS_LOCKED,%0),ifelse(u(%va/FN_HASATTRP,%0,CHECKOUT),C,L),-)]
			[ifelse(u(%va/IS_TAGGED,%0),T,-)]
			[ifelse(u(%va/IS_PUBLIC,%0),M,-)]
			[ifelse(u(%va/IS_PUBLISHED,%0),P,-)]
			[ifelse(u(%va/IS_SUMMARY,%0),S,-)]%b%b
			[u(%va/FN_STREXACT,get(%0/DESC),u(%va/FN_FLEXWIDTH,30))]
			[rjust(words(remove(children(%0),#-1)),5)]
			[rjust(
				mul(
					round(
						fdiv(
							words(remove(children(%0),#-1)),
							max(1,words(lcon(%va)))
						),
						2
					),
					100
				)%%,
				5
			)]%b%b
			[rjust(u(%va/FN_FIND-BBNUM,get(%0/POST_COMPLETE)),2)]%b
			[rjust(u(%va/FN_FIND-BBNUM,get(%0/POST_APPROVE)),2)]%b
			[rjust(u(%va/FN_FIND-BBNUM,get(%0/POST_DENY)),2)]
			[rjust(default(%0/TURNAROUND,0),5)]
			[rjust(
				[ifelse(
					u(%va/FN_HASATTR,%0,STAT_ART),
					[round(
						fdiv(
							fdiv(
								first(get(%0/STAT_ART)),
								rest(get(%0/STAT_ART))
							),
							86400
						),
						0
					)]d,
					-
				)],
				7
			)],
		[ljust(name(%0),6)]%b%b
			[u(%va/FN_STREXACT,get(%0/DESC),u(%va/FN_FLEXWIDTH,50))]%b
			[rjust(
				ifelse(
					u(%va/FN_MONITORCHECK,%0,%#),
					Yes,
					-
				),
				20
			)]
	)]
-

&FLAGS [v(JOB_VD)]=
	[ifelse(
		u(%va/IS_LOCKED,%0),
		u(%va/FN_FLAG,ifelse(u(%va/FN_HASATTRP,%0,CHECKOUT),CHECKED OUT,LOCKED),hr),
	)]
	[ifelse(
		u(%va/IS_PUBLIC,%0),
		u(%va/FN_FLAG,Myjobs),
	)]
	[ifelse(
		u(%va/IS_PUBLISHED,%0),
		u(%va/FN_FLAG,Published),
	)]
-

&FOOTER [v(JOB_VD)]=
	[switch(
		%0,
		,repeat(=,u(%va/FN_FLEXWIDTH,79)),
		center(| [ansi(u(me/FN_STYLE,COLOR_FOOTERTEXT),%0)] |,u(%va/FN_FLEXWIDTH,79),=)
	)]
-

&HEADER [v(JOB_VD)]=
	[switch(
		%0,
		,[repeat(=,u(%va/FN_FLEXWIDTH,79))],
		[center(| [ansi(u(me/FN_STYLE,COLOR_HEADERTEXT),%0)] |,u(%va/FN_FLEXWIDTH,79),=)]
	)]
-

&JOBLIST [v(JOB_VD)]=
	[setq(j,
		secure(
			ifelse(
				u(%va/FN_HASATTRP,%1,JOBS),
				lcstr(mid(get(%1/JOBS),0,20)),
				get(%va/JOBS_DEFAULT)
			)
		)
	)]
	[setq(1,
		ifelse(
			and(get(%0/DUE_ON),gt(secs(),get(%0/DUE_ON))),
			u(me/FN_STYLE,COLOR_JOBRED),
			switch(
				get(%0/PRIORITY),
				1,u(me/FN_STYLE,COLOR_JOBGREEN),
				2,u(me/FN_STYLE,COLOR_JOBYELLOW),
				3,u(me/FN_STYLE,COLOR_JOBRED),
				u(me/FN_STYLE,COLOR_JOBGREEN)
			)
		)
	)]
	[ifelse(
		u(%va/FN_ISNEW,%0,%1),
		[ansi(u(me/FN_STYLE,COLOR_NEWJOBMARKER),*)]%b,
		%b%b
	)]
	[ansi(%q1,
		[rjust(
			[last(name(%0))],
			5
		)]%b
		[iter(
			lnum(strlen(%qj)),
			[u(%va/DISPLAY_[mid(%qj,##,1)],%0,%1)]
		)]
	)]
-

&JOBSHEADER [v(JOB_VD)]=
	[u(%va/FN_HEADER,Anomaly Jobs [u(%va/VERSION)])]%r
	[setq(j,
		[secure(
			switch(
				u(%va/FN_HASATTRP,%0,JOBS),
				1,lcstr(mid(get(%0/JOBS),0,20)),
				u(%va/JOBS_DEFAULT)
			)
		)]
	)]
	[ansi(u(me/FN_STYLE,COLOR_JOBSHEADER),*%b%b
		[ljust(Job#,5)]
		[iter(
			lnum(strlen(%qj)),
			u(%va/HEADER_[mid(%qj,##,1)])
		)]
	)]%r
	[u(%va/FN_BREAK)]
-

&MYJOBLIST [v(JOB_VD)]=
	[ifelse(u(%va/FN_ISNEW,%0,%1),[ansi(u(me/FN_STYLE,COLOR_NEWJOBMARKER),*)],%b)]
	%b
	[setq(j,TDAS)]
	[rjust([last(name(%0))],5)]
	%b
	[iter(lnum(strlen(%qj)),u(%va/DISPLAY_[mid(%qj,##,1)],%0,%1))]
-

&MYJOBSHEADER [v(JOB_VD)]=
	[u(%va/FN_HEADER,
		Anomaly Jobs [u(%va/VERSION)]
	)]
	%r
	[setq(j,TDAS)]
	[ansi(u(me/FN_STYLE,COLOR_JOBSHEADER),ljust(
			*
			%b
			%b
			[ljust(Job#,5)]
			[iter(lnum(strlen(%qj)),u(%va/HEADER_[mid(%qj,##,1)]))],
		[u(%va/FN_FLEXWIDTH,79)])
	)]
	%r
	[u(%va/FN_BREAK)]
-

&MYBANNER [v(JOB_VD)]=
	[setq(0,%0)]
	[setq(x,u(%va/FN_ITEMIZE,map(%va/MAP_NAME,get(%q0/OPENED_BY),%b,|),|))]
	[ljust(
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Bucket:),10)]%b
		[u(%va/FN_BUCKETNAME,%q0)],div(u(%va/FN_FLEXWIDTH,80),2))]
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Due On:),12)]%b
		[ifelse(get(%q0/DUE_ON),
			ifelse(gt(secs(),get(%q0/DUE_ON)),
				OVERDUE!,
				convsecs(get(%q0/DUE_ON))),
			-
		)]
		%r
		[ljust(
			[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Title:),10)]%b
			[get(%q0/TITLE)],
			div(u(%va/FN_FLEXWIDTH,80),2)
		)]
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Assigned To:),12)]%b
		[ifelse(isdbref(get(%q0/ASSIGNED_TO)),name(get(%q0/ASSIGNED_TO)),Nobody)]
		%r
		[ljust(
			[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Opened On:),10)]%b
			[convsecs(get(%q0/OPENED_ON))],
			div(u(%va/FN_FLEXWIDTH,80),2)
		)]
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Status:),12)]%b
		[switch(get(%q0/PRIORITY),1,Green,2,Yellow,3,Red)]%b
		%r
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Opened By:),10)]%b
		%qx
-

&MYSUMMARY [v(JOB_VD)]=%r[u(%va/FN_BREAK,MySummary)]%r[u(parent(%0)/MYSUMMARY,%0)]
-

&READ [v(JOB_VD)]=
	[repeat(-,u(%va/FN_FLEXWIDTH,79))]%r
	[ifelse(
		u(%va/EDIT_ACCESS,%#),
		[ansi(u(me/FN_STYLE,COLOR_READJOBSTAMP),%[)]
			[ifelse(
				and(
					or(
						u(%va/IS_PUBLISHED,%1),
						switch(
							extract(get(%1/%0),3,1,|),
							u(%1/OPENED_BY),1,
							0),
						hasflag(%1/%0,no_inherit)
					),
					u(%va/IS_PUBLIC,%1)
				),
				ansi(u(me/FN_STYLE,COLOR_READJOBNUM),[rest(%0,_)]+),
				ansi(u(me/FN_STYLE,COLOR_READJOBNUM),[rest(%0,_)]-)
			)]
			[ansi(u(me/FN_STYLE,COLOR_READJOBSTAMP),%])]%b,
	)]
	[ansi(u(me/FN_STYLE,COLOR_READJOBSTAMP),[extract(get(%1/%0),4,1,|)]%b
		added on%b
		[convsecs(
			extract(
				get(%1/%0),2,1,|)
		)]:%b 
	)]
	[edit(last(get(%1/%0),|),@@PIPE@@,|)]
-

&STAFFSUM [v(JOB_VD)]=
	%r
	[repeat(-,u(%va/FN_FLEXWIDTH,79))]%r
	[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),DB#:),10)] %0[space(10)]
		[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Comments:),10)] [setr(z,words(lattr(%0/COMMENT_*)))]%r%r
	[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Players:),10)] %(Players contributing to this job%)%r
	[setq(y,
		setunion(
			iter(
				lattr(%0/COMMENT_*),
				extract(get(%0/##),4,1,|),%b,|
			),,|
		)
	)]
	[u(%va/fn_columns,%qy,20,|,11)]
	[ifelse(
		u(%va/FN_HASATTR,%0,LIST_READERS),
		%r[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Readers:),10)]%b
			%(Players who have read this job in the past%)%r
		[setq(z,
			iter(
				get(%0/LIST_READERS),
				first(##,|)
			)
		)]
		[u(%va/fn_columns,map(%va/MAP_NAME,%qz,%b,|),20,|,11)],
	)]
	%r
	[rjust(ansi(u(me/FN_STYLE,COLOR_BANNER),Stats:),10)]%r
	[u(%va/fn_columns,
		iter(
			ifelse(
				u(%va/FN_HASATTR,%0,LIST_STATS),
				sort(get(%0/LIST_STATS)),
			),
			[first(##,|)]%b[last(##,|)],%b,|
		),20,|,11
	)]%r
-

&SUMMARY [v(JOB_VD)]=%r[repeat(-,u(%va/FN_FLEXWIDTH,79))]%r[u(parent(%0)/SUMMARY,%0)]
-

#
# The color styles for WHITEBG - mostly all we need...
#

&COLOR_BANNER WHITEBG=c
-

&COLOR_BUCKETHEADER WHITEBG=c
-

&COLOR_FOOTERTEXT WHITEBG=x
-

&COLOR_HEADERTEXT WHITEBG=y
-

&COLOR_NEWJOBMARKER WHITEBG=hr
-

&COLOR_JOBSHEADER WHITEBG=c
-

&COLOR_JOBRED WHITEBG=r
-

&COLOR_JOBYELLOW WHITEBG=y
-

&COLOR_JOBGREEN WHITEBG=g
-

&COLOR_READJOBNUM WHITEBG=c
-

&COLOR_READJOBSTAMP WHITEBG=x
-

#
# The few WHITEBG overrides we need
#

&FLAGS WHITEBG=
	[ifelse(
		u(me/IS_LOCKED,%0),
		%[[ansi(r,ifelse(u(%va/FN_HASATTRP,%0,CHECKOUT),CHECKED OUT,LOCKED))]%],
	)]
	[ifelse(
		u(me/IS_PUBLIC,%0),
		%[Myjobs%],
	)]
	[ifelse(
		u(me/IS_PUBLISHED,%0),
		%[Published%],
	)]
-

&FOOTER WHITEBG=
	[ansi(x,switch(%0,,repeat(=,u(%va/FN_FLEXWIDTH,79)),center(| [stripansi(%0)] |,u(%va/FN_FLEXWIDTH,79),=)))]
-

&HEADER WHITEBG=
	[ansi(x,switch(%0,,[repeat(=,u(%va/FN_FLEXWIDTH,79))],[center(| [stripansi(%0)] |,u(%va/FN_FLEXWIDTH,79),=)]))]
-